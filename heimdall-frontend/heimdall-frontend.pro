# ----------------------------------------------------
# This file is generated by the Qt Visual Studio Add-in.
# ------------------------------------------------------

TEMPLATE = app
TARGET = heimdall-frontend

macx {
	message("")

	QT_FRAMEWORKS_DIR = $$(QT_FRAMEWORKS_DIR)
	isEmpty(QT_FRAMEWORKS_DIR) {
		message("QT_FRAMEWORKS_DIR not specified, using default:")
		QT_FRAMEWORKS_DIR = /System/Library/Frameworks
	}

	message("QT_FRAMEWORKS_DIR = $$QT_FRAMEWORKS_DIR")
	message("")

	QMAKE_MACOSX_DEPLOYMENT_TARGET = $$(QMAKE_MACOSX_DEPLOYMENT_TARGET)
	isEmpty(QMAKE_MACOSX_DEPLOYMENT_TARGET) {
		message("QMAKE_MACOSX_DEPLOYMENT_TARGET not specified, using default:")
		QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.4
	}

	message("QMAKE_MACOSX_DEPLOYMENT_TARGET = $$QMAKE_MACOSX_DEPLOYMENT_TARGET")
	message("")

	QMAKE_MAC_SDK = $$(QMAKE_MAC_SDK)
	isEmpty(QMAKE_MAC_SDK) {
		message("QMAKE_MAC_SDK not specified, using default:")
		QMAKE_MAC_SDK = /Developer/SDKs/MacOSX10.4.sdk
	}

	message("QMAKE_MAC_SDK = $$QMAKE_MAC_SDK")
	message("")

	QMAKE_MACOSX_DEPLOYMENT_TARGET = $$MACOSX_DEPLOYMENT_TARGET
	QMAKE_MAC_SDK = $$MAC_SDK

	PRIVATE_FRAMEWORKS.files = $$(QTFRAMEWORKSDIR)/QtCore.framework \
		$$(QTFRAMEWORKSDIR)//QtGui.framework $$(QTFRAMEWORKSDIR)/QtXml.framework
	PRIVATE_FRAMEWORKS.path = Contents/Frameworks

	QMAKE_BUNDLE_DATA += PRIVATE_FRAMEWORKS

	config += x86 x86_64 ppc

	isEmpty(OUTPUTDIR) {
		DESTDIR = /Applications
	} else {
		DESTDIR = $$OUTPUTDIR
	}

} else {
	win32 {	# It is recommended that Windows users compile via VS2010, but just in case...
		DESTDIR = ../Win32

		!isEmpty(OUTPUTDIR) {
			target.path = $$OUTPUTDIR
			INSTALLS += target
		}
	} else {
		DESTDIR = ../Linux

		isEmpty(OUTPUTDIR) {
			target.path = /usr/local/bin
		} else {
			target.path = $$OUTPUTDIR
		}

		INSTALLS += target
	}
}

unix:LIBS += -lz ../libpit/libpit-1.4.a
win32:LIBS += ../Win32/Release/lib/libpit.lib

QT += core gui xml
CONFIG += release
DEFINES += QT_LARGEFILE_SUPPORT
INCLUDEPATH += ./GeneratedFiles \
    ./GeneratedFiles/Release \
    ../libpit/Source \
    .
DEPENDPATH += .
MOC_DIR += ./GeneratedFiles/release
OBJECTS_DIR += release
UI_DIR += ./GeneratedFiles
RCC_DIR += ./GeneratedFiles
include(heimdall-frontend.pri)
